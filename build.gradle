plugins {
    id 'idea'
    id 'java'
//    id 'version-catalog'
    id 'maven-publish'
    id 'org.checkerframework' version '+'
    id 'io.freefair.lombok' version '5.+'
}

ext {
    ENV = System.getenv()
    VERSION = '0.1.0'
    BUILD = "${ENV.GITHUB_RUN_NUMBER ? (ENV.GITHUB_RUN_NUMBER ?: '0') : 'INTERNAL_BUILD'}"
}

group = 'org.pistonmc.api'
archivesBaseName = 'plugin-api'
version = "${VERSION}.${BUILD}"

repositories {
    mavenCentral()
    maven {
        name 'PistonAPI Common GitHub Registry'
        url 'https://maven.pkg.github.com/PistonMinecraft/PistonAPI-Common'
        credentials {
            username = ENV.GPR_USERNAME
            password = ENV.GPR_TOKEN
        }
    }
    maven {
        name 'forge-maven'
        url 'https://maven.minecraftforge.net/'
    }
}

configurations {
    implementation {
        canBeResolved = true
    }
}

dependencies {
    implementation libs.bundles.manifold.resources

    implementation libs.bundles.shared.libs
    implementation(libs.bundles.shared.nontransitive.libs) {
        transitive = false
    }

    implementation 'org.snakeyaml:snakeyaml-engine:2.2.1'

    annotationProcessor libs.bundles.manifold.processors
}

//catalog {
//    versionCatalog {
//        from(files('gradle/libs.versions.toml'))
//    }
//}

jar {
    manifest.attributes([
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'PistonMC',
            'Multi-Release': 'true',
    ])
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(configurations.implementation.collect(this::zipTree))
}

lombok {
    config['lombok.sneakyThrows.flagUsage'] = 'error'
    config['lombok.equalsAndHashCode.flagUsage'] = 'error'
}

tasks.withType(JavaCompile) {
    options.encoding 'UTF-8'
    options.compilerArgs.addAll(['-Xplugin:Manifold'])
}

checkerFramework {
    excludeTests = true
    checkers = [
            'org.checkerframework.checker.nullness.NullnessChecker',
            'org.checkerframework.common.value.ValueChecker',
    ]
}

publishing {
    repositories {
        maven {
            url "https://maven.pkg.github.com/PistonMinecraft/${project.name}"
            credentials {
                username = ENV.GPR_USERNAME
                password = ENV.GPR_TOKEN
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
//        maven(MavenPublication) {
//            artifactId = "${archivesBaseName}-catalog"
//            from components.versionCatalog
//        }
    }
}
